# TelegramQt@QT_VERSION_MAJOR@Config.cmake is generated by CMake from TelegramQtConfig.cmake.in.
# Any changed value in this file will be overwritten by CMake.

if(CMAKE_VERSION VERSION_LESS 3.9)
    message(FATAL_ERROR "TelegramQt requires at least CMake version 3.9")
endif()

# Valid modules: Core, Qml
set(_WANTED_COMPONENTS "${TelegramQt@QT_VERSION_MAJOR@_FIND_COMPONENTS}")
list (FIND _WANTED_COMPONENTS "Core" _CORE_INDEX)
if (${_CORE_INDEX} LESS 0)
    list(APPEND _WANTED_COMPONENTS Core)
endif()

foreach(module ${_WANTED_COMPONENTS})
    include("${CMAKE_CURRENT_LIST_DIR}/TelegramQt5${module}Targets.cmake")
endforeach()

unset(_WANTED_COMPONENTS)

# The code below is needed only for backward compatibility and will be removed from the release
get_target_property(TELEGRAM_QT@QT_VERSION_MAJOR@_LIBRARIES
    TelegramQt@QT_VERSION_MAJOR@::Core
    IMPORTED_LOCATION
)

if(NOT TELEGRAM_QT@QT_VERSION_MAJOR@_LIBRARIES)
    get_target_property(_IMPORTED_CONFIGURATIONS
        TelegramQt@QT_VERSION_MAJOR@::Core
        IMPORTED_CONFIGURATIONS
    )
    list(GET _IMPORTED_CONFIGURATIONS 0 _SELECTED_CONFIGURATION)
    get_target_property(TELEGRAM_QT@QT_VERSION_MAJOR@_LIBRARIES
        TelegramQt@QT_VERSION_MAJOR@::Core
        IMPORTED_LOCATION_${_SELECTED_CONFIGURATION}
    )
    unset(_IMPORTED_CONFIGURATIONS)
    unset(_SELECTED_CONFIGURATION)
endif()

get_target_property(TELEGRAM_QT@QT_VERSION_MAJOR@_INCLUDE_DIR
    TelegramQt@QT_VERSION_MAJOR@::Core
    INTERFACE_INCLUDE_DIRECTORIES
)
